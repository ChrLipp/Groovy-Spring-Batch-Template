apply plugin: 'groovy'
apply plugin:'application'
apply plugin: 'codenarc'

repositories {
	mavenCentral()
	maven { url "http://maven.springframework.org/release" }
}

dependencies {

	final SPRING_VERSION = '3.1.2.RELEASE'
	final BATCH_VERSION = '2.1.8.RELEASE'
	final SPOCK_VERSION = '0.6-groovy-1.8'
	final SLF4J_VERSION = '1.6.6'

	groovy group: 'org.codehaus.groovy', name: 'groovy-all', version: '1.8.7'

	compile group: 'org.springframework.batch', name: 'spring-batch-core', version: BATCH_VERSION

	// Spring Batch on top of Spring 3.1 (primarily to avoid xml configuration)
	// (latest version wins in conflict resolution)
	compile group: 'org.springframework', name: 'spring-tx', version: SPRING_VERSION
	compile group: 'org.springframework', name: 'spring-jdbc', version: SPRING_VERSION
//	compile group: 'org.springframework', name: 'spring-orm', version: SPRING_VERSION
//	compile group: 'org.springframework', name: 'spring-oxm', version: SPRING_VERSION
	compile group: 'cglib', name: 'cglib-nodep', version: '2.2.2'
	compile group: 'commons-dbcp', name: 'commons-dbcp', version: '1.4'
	compile group: 'com.h2database', name: 'h2', version: '1.3.168'
	
//	logging spring cleaning - use slf4j instead of commons-logging	
	runtime group: 'org.slf4j', name: 'jcl-over-slf4j', version: SLF4J_VERSION
//	runtime group: 'org.slf4j', name: 'slf4j-log4j12', version: SLF4J_VERSION
	runtime group: 'ch.qos.logback', name: 'logback-classic', version: '1.0.6'
	compile group: 'org.slf4j', name: 'slf4j-api', version: SLF4J_VERSION
	
	
	// beanbuilder dsl would be a nice addition, but it's dependency graph is awful
	//compile group: 'org.grails', name: 'grails-spring', version: '2.0.2'
	
	testCompile group: 'org.spockframework', name: 'spock-core', version: SPOCK_VERSION
	testCompile group: 'org.spockframework', name: 'spock-spring', version: SPOCK_VERSION
	testCompile group: 'org.springframework.batch', name: 'spring-batch-test', version: BATCH_VERSION
	testCompile group: 'org.springframework', name: 'spring-test', version: SPRING_VERSION
}

configurations {
	all*.exclude module: 'commons-logging' // we use slf4j instead
}

mainClassName = 'org.springframework.batch.core.launch.support.CommandLineJobRunner'

run {
	args 'launch-context.xml', 'job1'
}

task wrapper(type: Wrapper) {
	gradleVersion = '1.1'
}

codenarc {
	toolVersion = '0.17'
	configFile = new File("${project.projectDir.absolutePath}/gradle/codenarc/ruleset.groovy")
	
	// consumed by Jenkins Violations plugin
	reportFormat = 'xml'
	
	// don't let codenarc violations fail the build
	tasks.withType(CodeNarc).all { codeNarcTask ->
		codeNarcTask.ignoreFailures = true
	}
}

// works but pollutes the gradle output with warnings, disabled until its ready for prime time
//apply from: 'https://github.com/valkolovos/gradle_cobertura/raw/master/repo/gradle_cobertura/gradle_cobertura/1.0/coberturainit.gradle'
//cobertura {
//	coverageSourceDirs = [project.sourceSets.main.groovy.srcDirs] as Set
//}
